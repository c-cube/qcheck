= QCheck
:toc: macro
:toclevels: 4
:source-highlighter: pygments

QuickCheck inspired property-based testing for OCaml, and combinators to
generate random values to run tests on.


The documentation can be found https://c-cube.github.io/qcheck/[here].
This library spent some time in
https://github.com/vincent-hugot/iTeML[qtest], but is now
standalone again!
Note that @gasche's
http://gasche.github.io/random-generator/doc/Generator.html[generator library]
can be useful too, for generating random values.

toc::[]

== Use

See the documentation. I also wrote
http://cedeela.fr/quickcheck-for-ocaml.html[a blog post] that explains
how to use it and some design choices; however, be warned that the API
changed in lots of small ways (in the right direction, I hope) so the code
will not work any more.
<<examples>> is an updated version of the blog post's examples.

== Build

    $ make

You can use opam:

    $ opam install qcheck

== License

The code is now released under the LGPL license.

[[examples]]
== An Introduction to the Library

First, let's see a few tests.

=== List Reverse is Involutive

We write a random test for checking that `List.rev (List.rev l) = l` for
any list `l`:

[source,OCaml]
----
let test =
  QCheck.Test.make ~count:1000
    ~name:"list_rev_is_involutive"
   (list int)
   (fun l -> List.rev (List.rev l) = l);;

QCheck_r.Test.check_exn test;;
----

Now, let's run the test (the exact output will change at each run, because of
the random seed):

----
# QCheck_runner.run_tests [test];;
random seed: 415927808
success (ran 1 tests)
- : int = 0
----


=== Mirrors and Trees

In the first example, we applied the combinator `list` to
the random generator `small_int` (ints between 0 and 100), and
that generates lists of random integers.

`QCheck` provides many useful combinators to write
generators, especially for recursive types, algebraic types,
tuples.

Let's see how to generate random trees:

[source,OCaml]
----
type tree = Leaf of int | Node of tree * tree

let leaf x = Leaf x
let node x y = Node (x,y)

let tree_gen = QCheck.Gen.(sized @@ fix
  (fun self n -> match n with
    | 0 -> map leaf nat
    | n ->
      frequency
        [1, map leaf nat;
         2, map2 node (self (n/2)) (self (n/2))]
    ));;

(* generate a few trees, just to check what they look like: *)
QCheck.Gen.generate ~n:20 tree_gen;;

let arbitrary_tree =
  let open QCheck.Iter in
  let rec shrink_tree = function
    | Leaf i -> QCheck.Shrink.int i >|= leaf
    | Node (a,b) ->
      (shrink_tree a >|= fun a' -> node a' b)
      <+>
      (shrink_tree b >|= fun b' -> node a b')
  in
  QCheck.make tree_gen ~shrink:shrink_tree ;;

----

Here we write a random generator, `tree_gen`, using
the `fix` combinator. `fix` is *sized* (it's a function from `int` to
a random generator; in particular for size 0 it returns only leaves).
The `sized` combinator first generates a size, and applies its argument
to this size.

Other combinators include monadic abstraction, lifting functions,
generation of lists, arrays, and a choice function.

Then, we define `arbitrary_tree`, a `tree QCheck.arbitrary` value, which
contains everything needed for testing on trees:

- a random generator (mandatory)
- a printer (optional, not provided here, but it would be easy to add)
- a *shrinker* (optional, very useful), for trying to reduce big
  counter-examples to small counter-examples  that are usually
  more easy to understand. There are some combinators in `QCheck.Shrink`
  and `QCheck.Iter` to build those shrinking functions.

Now let's write a test using this generator:

[source,OCaml]
----

let rec mirror_tree (t:tree) : tree = match t with
  | Leaf _ -> t
  | Node (a,b) -> node (mirror_tree b) (mirror_tree a);;

let tree_infix (t:tree): int list =
  let rec aux acc t = match t with
    | Leaf i -> i :: acc
    | Node (a,b) ->
      aux (aux acc b) a
  in
  aux [] t  ;;

let test_mirror =
  QCheck.Test.make ~name:"mirror_tree" ~count:200
    arbitrary_tree
    (fun (t:tree) ->
       List.rev (tree_infix t) = tree_infix (mirror_tree t))  ;;

QCheck_runner.run_tests [test_mirror];;

----

=== Runners

The module `QCheck_runner` defines several functions to run tests, including
compatibility with `OUnit`.
The easiest one is probably `run_tests`, but if you write your tests in
a separate executable you can also use `run_tests_main` which parses
command line arguments and exits with `0` in case of success,
or an error number otherwise.

=== Integration within OUnit

http://ounit.forge.ocamlcore.org/[OUnit] is a popular unit-testing framework
for OCaml.
QCheck provides some helpers, in `QCheck_runner`, to convert its random tests
into OUnit tests that can be part of a wider test-suite.

[source,OCaml]
----
let passing =
  QCheck.Test.make ~count:1000
    ~name:"list_rev_is_involutive"
    QCheck.(list small_int)
    (fun l -> List.rev (List.rev l) = l);;

let failing =
  QCheck.Test.make ~count:10
    ~name:"fail_sort_id"
    QCheck.(list small_int)
    (fun l -> l = List.sort compare l);;

let _ =
  let open OUnit in
  run_test_tt_main
    ("tests" >:::
       List.map QCheck_runner.to_ounit_test [passing; failing])

----
